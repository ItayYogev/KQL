CloudAppEvents
// Columns creation
| extend Region = tostring(RawEventData.awsRegion)
| extend RequestID = tostring(RawEventData.requestID)
| extend AccountID = tostring(RawEventData.userIdentity.accountId)
| extend Auditor_Role = tostring(RawEventData.userIdentity.sessionContext.sessionIssuer.userName)
| extend UserName = tostring(RawEventData.userIdentity.userName)
| extend DoesMFAAuthenticated = tostring(RawEventData.userIdentity.sessionContext.attributes.mfaAuthenticated)
| extend operation = tostring(RawEventData.userIdentity.type)
| extend requestParameters = tostring(RawEventData.requestParameters)
| extend environment = case(
    AccountID == "Insert Account ID Here", "Insert Account Name Here",
    "unknown" // default case if none of the above matches
)
// Where Cases:
// by platform
| where Application contains "AWS" or Application == "Amazon Web Services"
// others
| where AccountDisplayName contains "Insert User Name Here"
//
| project
    //my Columns
	Region, RequestID, AccountID, Auditor_Role, UserName, operation, environment, requestParameters, DoesMFAAuthenticated,
    //identifiers
	Timestamp, AccountId, AccountDisplayName, OSPlatform, IPAddress, CountryCode, City, ISP, UserAgent, ReportId, AccountType,
    //actions
	ActionType, IsAdminOperation, ActivityObjects, ObjectName, RawEventData, AdditionalFields, UncommonForUser
// Remove duplicates based on conditions
| order by Timestamp
| extend PrevTimestamp = prev(Timestamp), PrevActionType = prev(ActionType), PrevAccountDisplayName = prev(AccountDisplayName)
| extend TimeGap = datetime_diff('second', Timestamp, PrevTimestamp)
| where not (TimeGap < 2 and ActionType == PrevActionType and AccountDisplayName == PrevAccountDisplayName)
// Final projection and ordering
| project
    Region, RequestID, AccountID, Auditor_Role, UserName, operation, environment, requestParameters, DoesMFAAuthenticated,
    Timestamp, AccountId, AccountDisplayName, OSPlatform, IPAddress, CountryCode, City, ISP, UserAgent, ReportId, AccountType,
    ActionType, IsAdminOperation, ActivityObjects, ObjectName, RawEventData, AdditionalFields, UncommonForUser
| summarize arg_max(Timestamp, *) by RequestID
| order by Timestamp desc
